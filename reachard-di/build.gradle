plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.android'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

android {
    namespace 'com.burakkuyucu.reachard'
    compileSdk 33

    defaultConfig {
        minSdkVersion 1
        targetSdkVersion 33

        consumerProguardFiles("consumer-proguard-rules.pro")
    }

    buildTypes.all {
        consumerProguardFiles "consumer-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    resourcePrefix "reachard_"
}

dependencies {
    // Unit Test
    testImplementation "junit:junit:${versions.junit}"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.kuyucuburak'
                artifactId = 'Reacher'
                version = reachardDiVersionName

                afterEvaluate {
                    from components.release
                }
            }
        }
    }
}
